%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%     Exercise Set 7: Induction, Recursion, and Iteration
%     (Solutions)
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

induction: THEORY
BEGIN

  %
  % Exercise 1
  %

  % Assume the following definitions from ints@factorial and the PVS prelude
  %  factorial(n): RECURSIVE posnat =
  %    IF n = 0 THEN 1
  %    ELSE n*factorial(n-1) 
  %  ENDIDF 
  %  MEASURE n
  %
  %  even?(i): bool = EXISTS j: i = j * 2

  IMPORTING ints@factorial  

  factorial_even : LEMMA
    FORALL (n:above(1)) : 
      even?(factorial(n))

%|- factorial_even : PROOF
%|- (spread (induct "n")
%|-  ((grind)
%|-   (then (skeep :preds? t) (expand "factorial" 1) (expand "even?")
%|-    (skolem -2 "J") (replaces -2) (inst 1 "J*(ja+1)") (assert))))
%|- QED

  %
  % Exercise 2
  %

  factorial_ge : LEMMA
    FORALL (n:nat): factorial(n) >= n

%|- factorial_ge : PROOF
%|- (spread (induct "n")
%|-  ((grind)
%|-   (then (skeep) (expand "factorial" 1)
%|-    (spread (case "factorial(j) >= 1")
%|-     ((then (mult-by -1 "j+1") (assert)) (assert))))))
%|- QED

  %
  % Exercise 3
  %

  m,n : VAR nat

  % Ackermann
  ack(m,n) : RECURSIVE nat =
    IF    m = 0 THEN n+1
    ELSIF n = 0 THEN ack(m-1,1)
    ELSE ack(m-1,ack(m,n-1))
    ENDIF
  MEASURE lex2(m,n)

  % Type the Emacs command M-x tcp to automatically prove the TCCs that concern ack_gt_m_n
  ack_gt_m_n : RECURSIVE JUDGEMENT
    ack(m,n) HAS_TYPE above(m+n)

  ack_gt : LEMMA
    ack(m,n) > m+n

%|- ack_gt : PROOF
%|- (grind)
%|- QED

  %
  % Exercise 4
  %

  %   The PVS prelude includes the following definitions:
  %
  %   expt(r, n): RECURSIVE real =
  %     IF n = 0 THEN 1
  %     ELSE r * expt(r, n-1)
  %     ENDIF
  %    MEASURE n;
  %
  %  An imperative version of expt in pseudo-code:
  %  function expt_it(x:real,n:nat):nat {
  %    a := 1;
  %    // a = expt(x,0)
  %    for (i:=1; i <= n; i++) {
  %    // invariant: a = expt(x,i)
  %      a := a*x;
  %    }
  %    return a;
  %    // post: a = expt(x,n)
  %  }

  IMPORTING structures@for_iterate

  expt_it(x:real,n:nat): real =
    for[real](1,n,1,LAMBDA(i:subrange(1,n),a:real):a*x)

  % Invariant is LAMBDA(i:upto(n),a:real): a = expt(x,i)
  expt_it_sound : LEMMA
    FORALL(x:real,n:nat): expt_it(x,n) = expt(x,n)

%|- expt_it_sound : PROOF
%|- (then (skeep) (expand "expt_it") (lemma "for_induction[real]")
%|-  (inst? -1) (inst -1 "LAMBDA(i:upto(n),a:real): a = expt(x,i)") (grind))
%|- QED

  %
  % Exercise 5
  %

  % Sample inductive definitions, use M-x ppe to see automatically
  % generated induction schemas

  even(n:nat): INDUCTIVE bool = 
    n = 0 OR (n > 1 AND even(n - 2))
  
  we_are_even: LEMMA
    even(n) IMPLIES even?(n)

%|- we_are_even : PROOF
%|- (then (rule-induct "even") (skeep) (expand "even?")
%|-  (spread (split)
%|-   ((then (inst 1 "0") (assert))
%|-    (then (flatten) (skeep -2) (inst 1 "j+1") (assert)))))
%|- QED

END induction
